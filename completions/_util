#compdef util

# vim: set ft=zsh:

_util() {
  if [ ! -d "$SU_SCRIPTS_PATH" ]; then
    return
  fi

  # Captures all arguments and suggests the files/folders in
  # $SU_SCRIPTS_PATH. If a file is found, the files in the
  # current folder are suggested
  #
  # util command subcommand -> tries to find $SU_SCRIPTS_PATH/command/subommand

  _arguments \
    '*::arg:->args'

  local all_subcommands_but_the_last=("${(j:/:)words[1,CURRENT-1]}")
  local last_command="$words[CURRENT]"
  local scripts_path_with_arguments="$SU_SCRIPTS_PATH/$all_subcommands_but_the_last"

  local files=($(
    find "$scripts_path_with_arguments" \
      -maxdepth 1 \
      -mindepth 1 \
      -name "$last_command*" \
      -follow \
      -executable \
      -printf "%f\n" \
      2>/dev/null \
      | xargs -I{} sh -c 'echo {} | rev | cut -f2- -d "." | rev'
    ))

  if [ ${#files[@]} -eq 0 ]; then
    _files
  else
    _describe 'subcommand' files
  fi
}

_util "$@"
